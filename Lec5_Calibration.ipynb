{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "f1bd2c98",
   "metadata": {},
   "source": [
    "# Calibration\n",
    "\n",
    "Calibration is a procedure to determine parameter values of a quantitative model so that the model replicates key empirical regularities.  \n",
    "\n",
    "In our model, the parameter values to be set include:\n",
    "\n",
    "- the discount factor $\\beta$\n",
    "- the utility weight on consumption $\\omega$\n",
    "- the capital share $\\alpha$\n",
    "- the depreciation rate $\\delta$\n",
    "- the income replacement rate $\\theta$\n",
    "\n",
    "\n",
    "---\n",
    "\n",
    "## How?\n",
    "\n",
    "Some parameters are taken directly from data or estimated outside the model.  \n",
    "For example:\n",
    "- The capital share $\\alpha$ and depreciation rate $\\delta$ are often obtained from national accounts data.\n",
    "- If the model includes a stochastic income process, its parameters are usually estimated using micro-level income data.\n",
    "\n",
    "Other parameters are calibrated so that the model replicates **targeted moments**—key empirical patterns.  \n",
    "For example, in macroeconomic models:\n",
    "- $\\beta$ is often chosen so that the model implies a capital-output ratio $K/Y$ close to the one observed in data.\n",
    "\n",
    "---\n",
    "\n",
    "## Two-Step Calibration\n",
    "\n",
    "This approach is called two-step calibration:\n",
    "\n",
    "1. **Step 1**: Set parameters based on external data or literature.\n",
    "2. **Step 2**: Calibrate remaining parameters so that the model replicates targeted moments.\n",
    "\n",
    "Researchers must decide which parameters fall into each step. This depends on:\n",
    "- Whether the parameter is directly observable\n",
    "- Whether reliable empirical estimates are available\n",
    "- What is central to the research question\n",
    "\n",
    "---\n",
    "\n",
    "## Example: Target Selection\n",
    "\n",
    "Suppose we consider the pension income replacement rate $\\theta$. It could be directly set using institutional data. However, many studies calibrate $\\theta$ **within the model** so that it reproduces the **pension-to-GDP ratio**. This is particularly important when analyzing:\n",
    "- The macroeconomic consequences of demographic change\n",
    "- Fiscal impacts of pension reform\n",
    "\n",
    "In those cases, the pension-to-GDP ratio is the key moment to match, not the raw value of $\\theta$ itself.\n",
    "\n",
    "---\n",
    "\n",
    "## Calibration Strategy in This Model\n",
    "\n",
    "We follow this specific calibration procedure:\n",
    "\n",
    "### Step 1: Externally Set Parameters\n",
    "\n",
    "Set based on empirical consensus:\n",
    "- $\\alpha = 0.33$\n",
    "- $\\delta = 0.1$\n",
    "- $\\sigma = 2.0$\n",
    "\n",
    "> Note: The depreciation rate $\\delta$ depends on the model’s period length.\n",
    "\n",
    "### Step 2: Internally Calibrated Parameters\n",
    "\n",
    "Choose values to match targeted moments:\n",
    "- $\\beta$: calibrated so that $K/Y = 4.0$\n",
    "- $\\omega$: chosen so that households work 1/3 of their available time\n",
    "- $\\theta$: set so that the model replicates a pension-to-GDP ratio of 0.1 (i.e., 10% of GDP is spent on pensions)\n",
    "\n",
    "---\n",
    "\n",
    "## Algorithm Outline\n",
    "\n",
    "To implement calibration, we add an **outer loop** around the model solution:\n",
    "\n",
    "1. Set $\\alpha = 0.33$, $\\delta = 0.1$, and $\\sigma=2.0$.\n",
    "2. Make initial guesses for $(\\beta, \\omega, \\theta)$\n",
    "3. Solve the general equilibrium model\n",
    "4. Compute the implied values of the targeted moments\n",
    "5. If the model moments do not match the targets, update the guesses and repeat until convergence\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "e667a843",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "solve_lifecycle (generic function with 1 method)"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "# set household optimization routine.\n",
    "function solve_lifecycle(r,w,τ,β,ω,θ)\n",
    "\n",
    "    # parameters\n",
    "    σ=2.0 # additional parameter (curvature)\n",
    "\n",
    "    # discretization for asset space\n",
    "    mina = 1.e-5 # lower bound (close to zero but not exactly (to avoid c=0))\n",
    "    maxa = 10.0  # upper bound\n",
    "    na   = 1000  # number of grid\n",
    "    grida = collect(LinRange(mina,maxa,na)); # construct discretized asset space\n",
    "\n",
    "    # age related parameters\n",
    "    J=61  # length of lifecycle\n",
    "    JR=46 # timing of retirement\n",
    "\n",
    "    # value function and policy functions\n",
    "    V = zeros(na,J); # value function V(a,j)\n",
    "    Va = zeros(na);  # vector for grid search\n",
    "    apol = zeros(na,J); # policy function for saving (value) a(a,J)\n",
    "    apol_arg = zeros(Int,na,J); # policy function for saving (index over asset space, grida)\n",
    "    cpol = zeros(na,J)  # policy function for consumption c(a,J)\n",
    "    hpol = zeros(na,J) # policy function for labor \n",
    "\n",
    "    penalty=-1.e+8\n",
    "\n",
    "    for jc in J:-1:1 # solve backward\n",
    "        acc_start=1 # for speed up\n",
    "        for ac in 1:na\n",
    "            if (jc==J) # compute final consumption given a\n",
    "                c = (1.0+r)*grida[ac]+w*θ\n",
    "                if (c>0)\n",
    "                    V[ac,jc]=((c^ω)^(1.0-σ))/(1.0-σ)\n",
    "                else # \"punish\" c<=0\n",
    "                    V[ac,jc]=penalty\n",
    "                end\n",
    "                cpol[ac,jc]=c; # store it in policy function for c\n",
    "            elseif (JR < jc) && (jc < J) # retirement period (except for the final period)\n",
    "                Va.=penalty;\n",
    "                v0=penalty\n",
    "                for acc in acc_start:na\n",
    "                    c = (1.0+r)*grida[ac]-grida[acc]+w*θ\n",
    "                    if (c>0)\n",
    "                        Va[acc] = ((c^ω)^(1.0-σ))/(1.0-σ) + β*V[acc,jc+1]\n",
    "                    else\n",
    "                        Va[acc] = penalty\n",
    "                    end\n",
    "\n",
    "                    if (Va[acc]>v0)\n",
    "                        v0=Va[acc]\n",
    "                        acc_start=acc\n",
    "                    else\n",
    "                        break\n",
    "                    end\n",
    "                end\n",
    "                arg = acc_start\n",
    "                apol_arg[ac,jc]=arg\n",
    "                apol[ac,jc] = grida[arg]\n",
    "                V[ac,jc] = Va[arg]\n",
    "                cpol[ac,jc]=(1.0+r)*grida[ac]-grida[arg];\n",
    "            else\n",
    "                Va.=penalty;\n",
    "                v0=penalty\n",
    "                for acc in acc_start:na\n",
    "                    l = (1.0-ω) * ((1.0+r)*grida[ac]-grida[acc] + w)/w\n",
    "                    c = (1.0-τ)*w*(1.0-l)+(1.0+r)*grida[ac]-grida[acc]\n",
    "                    if (c>0 && l>0)\n",
    "                        Va[acc] = ((c^ω * l^(1.0-ω))^(1.0-σ))/(1.0-σ) + β*V[acc,jc+1]\n",
    "                    else\n",
    "                        Va[acc] = penalty\n",
    "                    end\n",
    "\n",
    "                    if (Va[acc]>v0)\n",
    "                        v0=Va[acc]\n",
    "                        acc_start=acc\n",
    "                    else\n",
    "                        break\n",
    "                    end\n",
    "\n",
    "                end\n",
    "                arg = acc_start\n",
    "                acc_start = arg\n",
    "                apol_arg[ac,jc]=arg\n",
    "                apol[ac,jc] = grida[arg]\n",
    "                V[ac,jc] = Va[arg]\n",
    "                l = (1.0-ω) * ((1.0+r)*grida[ac]-grida[arg] + w)/w\n",
    "                cpol[ac,jc]=(1.0-τ)*w*(1.0-l) + (1.0+r)*grida[ac]-grida[arg];\n",
    "                hpol[ac,jc] = 1.0-l\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # find asset profile aj\n",
    "    aj = zeros(J);\n",
    "    aj[1]=grida[1]; # start from no-asset\n",
    "    a_arg = ones(Int,J);\n",
    "    arg_yesterday=1;\n",
    "    for jc in 2:J\n",
    "        arg_yesterday=a_arg[jc-1];\n",
    "        arg = apol_arg[arg_yesterday,jc-1];\n",
    "        aj[jc]=grida[arg];\n",
    "        a_arg[jc]=arg;\n",
    "    end\n",
    "\n",
    "    # find consumption\n",
    "    cj = zeros(J);\n",
    "    hj = zeros(J)\n",
    "    for jc in 1:J\n",
    "        arg = a_arg[jc];\n",
    "        cj[jc]=cpol[arg,jc]\n",
    "        hj[jc]=hpol[arg,jc]\n",
    "    end\n",
    "\n",
    "    muj = fill(1.0/J,J);\n",
    "\n",
    "    return aj,hj,muj,J,JR,θ\n",
    "    \n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "e1467b29",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "solve_GE (generic function with 1 method)"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "function solve_GE(ini_K,ini_H,ini_τ,β,ω,θ)\n",
    "    # solve GE given guesses on aggregate variables, given parameters.\n",
    "    # return model-implied moments\n",
    "\n",
    "    # parameters\n",
    "    α=0.33;\n",
    "    δ=0.1;\n",
    "\n",
    "    # step 1. Make initial guesses\n",
    "    K = ini_K;\n",
    "    H = ini_H;\n",
    "    τ = ini_τ;\n",
    "    imp_K = K;\n",
    "    imp_H = H;\n",
    "    imp_τ = τ;\n",
    "\n",
    "    # returned variables needs to be set in advance. otherwise an error arises.\n",
    "    K_Y=1.0\n",
    "    Work=1.0\n",
    "    P_Y=1.0\n",
    "\n",
    "    dif=[1.0, 1.0, 1.0]; # for (K,H,τ)\n",
    "    tol=[1.e-2, 1.e-2, 1.e-2]; # for (K,H,τ)\n",
    "\n",
    "    iter=1;\n",
    "    max_iter=1000; # for when something is wrong.\n",
    "\n",
    "    while any(dif .> tol)\n",
    "\n",
    "        # (step 4: update)\n",
    "        adj=0.1;\n",
    "        K = imp_K*adj + K*(1.0-adj)\n",
    "        H = imp_H*adj + H*(1.0-adj)\n",
    "        τ = imp_τ*adj + τ*(1.0-adj)\n",
    "\n",
    "        # step 2. Household problems\n",
    "        r = (K/H)^(α-1.0) - δ\n",
    "        w = (K/H)^α\n",
    "        aj,hj,muj,J,JR,θ = solve_lifecycle(r,w,τ,β,ω,θ)\n",
    "\n",
    "        # step 3. Compute the implied aggregate variables\n",
    "        imp_K = sum(aj.*muj)\n",
    "        imp_H = sum(hj.*muj)\n",
    "        imp_τ = θ*sum(muj[JR+1:J])/imp_H\n",
    "\n",
    "        # step 4. Check if the guesses are right. If not, update.\n",
    "        dif_K = abs(K-imp_K)/K\n",
    "        dif_H = abs(H-imp_H)/H\n",
    "        dif_τ = abs(τ-imp_τ)/τ\n",
    "\n",
    "        dif .= [dif_K,dif_H,dif_τ];\n",
    "\n",
    "        # println(\"Iter:\", iter, \"max(dif):\",maximum(dif))\n",
    "\n",
    "        if (iter>max_iter)\n",
    "            break\n",
    "        end\n",
    "        iter+=1;\n",
    "\n",
    "        r = (K/H)^(α-1.0) - δ\n",
    "        w = (K/H)^α\n",
    "        Y = K^α * H^(1.0-α)\n",
    "        Total_Pen = w*θ*sum(muj[JR+1:J])\n",
    "\n",
    "        # compute targeted moments\n",
    "        K_Y = K/Y\n",
    "        Work = sum(hj[1:JR])/JR # this is okay as, in this case, age distribution is uniform\n",
    "        P_Y = Total_Pen/Y\n",
    "\n",
    "    end\n",
    "\n",
    "    return K_Y,Work,P_Y,K,H,τ\n",
    "\n",
    "\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "id": "f1767c62",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "(iter, dif) = (154, [0.0004998129527147288, 0.039486948633768365, 0.031172605568279416])\n",
      "K/Y = 4.00200\n",
      "Work = 0.34303\n",
      "P/Y = 0.10312\n",
      "beta = 0.88083\n",
      "omega = 0.40192\n",
      "theta = 0.10946\n"
     ]
    }
   ],
   "source": [
    "# calibration (2nd step)\n",
    "using Printf\n",
    "# 0. set target\n",
    "Targ_KY = 4.0\n",
    "Targ_Work = 0.33\n",
    "Targ_PY = 0.1\n",
    "imp_KY=1.0\n",
    "imp_Work=1.0\n",
    "imp_PY=1.0\n",
    "\n",
    "# 1. make initial guesses\n",
    "β=0.98\n",
    "ω=0.5\n",
    "θ=0.1\n",
    "\n",
    "# 2. solve GE with that, obtaining model-implied moments given the guess.\n",
    "ini_K = 6.0;\n",
    "ini_H = 0.5;\n",
    "ini_τ = 0.1\n",
    "\n",
    "dif=[1.0, 1.0, 1.0]; # for (K_Y,Work,P_Y)\n",
    "tol=[4.e-2, 4.e-2, 4.e-2]; # for (K_Y,Work,P_Y)\n",
    "\n",
    "iter=1\n",
    "max_iter=500\n",
    "\n",
    "while any(dif.>tol)\n",
    "    \n",
    "    imp_KY,imp_Work,imp_PY,K,H,τ = solve_GE(ini_K,ini_H,ini_τ,β,ω,θ)\n",
    "\n",
    "    # Check if model-implied moment is close enough to its target. If not, update.\n",
    "    dif_KY = abs(Targ_KY-imp_KY)/Targ_KY\n",
    "    dif_Wo = abs(Targ_Work-imp_Work)/Targ_Work\n",
    "    dif_PY = abs(Targ_PY-imp_PY)/Targ_PY\n",
    "\n",
    "    dif .= [dif_KY,dif_Wo,dif_PY];\n",
    "\n",
    "    if (iter>max_iter)\n",
    "        break\n",
    "    end\n",
    "    # @printf(\"iter = %d    dif = %.5f\\n\", iter, maximum(dif))\n",
    "    iter+=1;\n",
    "\n",
    "    # update\n",
    "    denom = 100.0\n",
    "\n",
    "    if (imp_KY>Targ_KY) # save too much\n",
    "        β=β*(1.0-dif_KY/denom)\n",
    "    else\n",
    "        β=β*(1.0+dif_KY/denom)\n",
    "    end\n",
    "\n",
    "    if (imp_Work>Targ_Work) # work too much\n",
    "        ω=ω*(1.0-dif_Wo/denom)\n",
    "    else\n",
    "        ω=ω*(1.0+dif_Wo/denom)\n",
    "    end\n",
    "\n",
    "    if (imp_PY>Targ_PY) # give too much pension\n",
    "        θ=θ*(1.0-dif_PY/denom)\n",
    "    else\n",
    "        θ=θ*(1.0+dif_PY/denom)\n",
    "    end\n",
    "\n",
    "    ini_K=K\n",
    "    ini_H=H\n",
    "    ini_τ=τ\n",
    "\n",
    "end\n",
    "\n",
    "\n",
    "@show iter, dif\n",
    "using Printf\n",
    "@printf(\"K/Y = %.5f\\nWork = %.5f\\nP/Y = %.5f\\n\", imp_KY,imp_Work,imp_PY)\n",
    "@printf(\"beta = %.5f\\nomega = %.5f\\ntheta = %.5f\\n\", β,ω,θ)\n",
    "\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.4",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
